@precedence { logic @left }

@top Program { Query }

Query {
    Name ( WhereClause | OrderClause  | LimitClause | SelectClause | RenderClause )*
}

commaSep<content> { content ("," content)* }

WhereClause { Where LogicalExpr }
OrderClause { Order By Name OrderDirection? }
LimitClause { Limit Number }
SelectClause { Select commaSep<Name> }
RenderClause { Render (PageRef | String) }

OrderDirection {
  "desc" | "asc"
}

Value { Number | String | Bool | Regex | Null | List }

LogicalExpr { FilterExpr (And FilterExpr)* }

// AndExpr { FilterExpr (And FilterExpr)* }

FilterExpr {
  Name "<" Value
| Name "<=" Value
| Name "=" Value
| Name "!=" Value
| Name ">=" Value
| Name ">" Value
| Name "=~" Value
| Name "!=~" Value
| Name "in" Value
}

List { "[" commaSep<Value> "]" }

@skip { space }

Bool {
  "true" | "false"
}


@tokens {
  space { std.whitespace+ }
  Name { (std.asciiLetter | "-" | "_")+ }

  Where { "where" }
  Order { "order" }
  By { "by" }
  Select { "select" }
  Render { "render" }
  Limit { "limit" }
  And { "and" }
  Null { "null" }

  String {
    ("\"" | "“" | "”") ![\"”“]* ("\"" | "“" | "”")
  }
  PageRef {
    "[" "[" ![\]]* "]" "]"
  }
  Regex { "/" ( ![/\\\n\r] | "\\" _ )* "/"? }

  Number { std.digit+ }

  @precedence { Where, Order, By, Select, Render, Limit, And, Null, Name }
}
